
#Использовать "../../../pkg/edtcoverage"
#Использовать "../internal/localization"

Перем Лог;
Перем ЛокализованныеСтроки;

#Область КомандаПриложения

Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Опция("i infobase", "DefAlias", ЛокализованныеСтроки.ПредметОтладки)
		.ТСтрока();

	Команда.Опция("o output", , ЛокализованныеСтроки.ФайлРезультатаCSV)
		.ТСтрока()
		.Обязательный();

	Команда.Опция("u debugger", "http://localhost:1550", ЛокализованныеСтроки.АдресОтладчика)
		.ТСтрока();
	
	Команда.Опция("p password", , ЛокализованныеСтроки.ПарольОтладчика)
		.ТСтрока()
		.ВОкружении("DBGS_PASSWORD");

КонецПроцедуры

#Область ОбработчикиСобытий

Процедура ПриСозданииОбъекта() Экспорт
	Лог = ПараметрыПриложения.Лог();
	ЛокализованныеСтроки = ЛокализованныеРесурсыКомандаСтарт.ЛокализованныеСтроки();
КонецПроцедуры

#КонецОбласти

Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	ПредметОтладки = Команда.ЗначениеОпции("infobase");
	ИмяФайла       = Команда.ЗначениеОпции("output");
	СерверОтладки  = Команда.ЗначениеОпции("debugger");

	ПарольОтладки  = Команда.ЗначениеОпции("password");

	Лог = ПараметрыПриложения.Лог();
	Если Команда.ЗначениеОпции("debug") Тогда
		ПараметрыПриложения.ВключитьРежимОтладки();
	КонецЕсли;

	МенеджерПокрытия = Новый МенеджерПокрытияEDT();
	МенеджерПокрытия.НайтиКаталогEDT();

	КаталогEDT = МенеджерПокрытия.КаталогEDT();
	Лог.Отладка(СтрШаблон(ЛокализованныеСтроки.СообщениеПутьМодуляEDT, КаталогEDT));

	МенеджерПокрытия.УстановитьПараметрыОтладки(ПредметОтладки, СерверОтладки);

	ИдентификаторПроцесса = МенеджерПокрытия.ЗапуститьСборПокрытия(ИмяФайла);
	Лог.Отладка(СтрШаблон(ЛокализованныеСтроки.СообщениеИдентификаторПроцесса, ИдентификаторПроцесса));

	СоздатьPIDФайл(ИдентификаторПроцесса);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьPIDФайл(ИдентификаторПроцесса)

	ИмяФайла = СтрШаблон("coverage.%1.pid", XMLСтрока(ИдентификаторПроцесса));
	ПолноеИмяФайла = ОбъединитьПути(КаталогВременныхФайлов(), ИмяФайла);
	
	ЗаписьТекста = Новый ЗаписьТекста;
	ЗаписьТекста.Открыть(ПолноеИмяФайла);
	ЗаписьТекста.Закрыть();

	Лог.Информация(СтрШаблон(ЛокализованныеСтроки.СообщениеСозданPIDФайл, ПолноеИмяФайла));

КонецПроцедуры

#КонецОбласти
