
#Использовать "../../../internal/utils"
#Использовать fs

Процедура ПолучитьОтчетGenericCoverage(КаталогПроекта, КаталогИсходниковКонфигурации, ФайлВывода) Экспорт

	КаталогПриложения = ПараметрыПриложения.КаталогПриложения();

	ПутьКлассовBSL = ОбъединитьПути(КаталогПриложения, "vendor", "bsl-language-server", "*");
	КонфигурацияАнализа = ОбъединитьПути(КаталогПриложения, "configs", "bsl-lsp-coverage.json");

	ПараметрыПриложенияJava = УтилитыПриложения.НовыйПараметрыПриложенияJava();
	ПараметрыПриложенияJava.ОсновнойКласс = "com.github._1c_syntax.bsl.languageserver.BSLLSPLauncher";
	ПараметрыПриложенияJava.ПутиКлассов.Добавить(ПутьКлассовBSL);

	КаталогРезультатов = ПолучитьИмяВременногоФайла();
	ФС.ОбеспечитьКаталог(КаталогРезультатов);

	Команда = УтилитыПриложения.КомандаПриложениеJava(ПараметрыПриложенияJava);
	Команда.ПоказыватьВыводНемедленно(Истина);
	Команда.ДобавитьПараметр("--analyze");
	Команда.ДобавитьПараметр("--reporter genericCoverage");
	Команда.ДобавитьПараметр(СтрШаблон("--srcDir ""%1""", КаталогИсходниковКонфигурации));
	Команда.ДобавитьПараметр(СтрШаблон("--outputDir ""%1""", КаталогРезультатов));
	Команда.ДобавитьПараметр(СтрШаблон("--workspaceDir ""%1""", КаталогПроекта));
	Команда.ДобавитьПараметр(СтрШаблон("--configuration ""%1""", КонфигурацияАнализа));
	Команда.ДобавитьПараметр("--silent");
	Команда.Исполнить();

	ФайлПокрытияXML = ОбъединитьПути(КаталогРезультатов, "genericCoverage.xml");

	КопироватьФайл(ФайлПокрытияXML, ФайлВывода);
	
	УдалитьФайлы(КаталогРезультатов);

КонецПроцедуры
